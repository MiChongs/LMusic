plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp' version '1.8.0-1.0.9'
}

def keystorePropsFile = rootProject.file("keystore.properties")
def keystoreProps = new Properties()
keystoreProps.load((new FileInputStream(keystorePropsFile)))

android {
    compileSdkVersion 33

    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }

    namespace 'com.lalilu'

    defaultConfig {
        applicationId "com.lalilu.lmusic"
        minSdk 21
        targetSdk 33

        versionCode 42
        versionName "1.5.4"
        vectorDrawables {
            useSupportLibrary true
        }
//        ndk {
//            abiFilters "armeabi-v7a"
//        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProps['storeFile'])
            storePassword keystoreProps['storePassword']
            keyAlias keystoreProps['keyAlias']
            keyPassword keystoreProps['keyPassword']
        }
    }

    buildTypes {
        release {
            resValue "string", "app_name", "@string/app_name_release"

            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }

        beta {
            resValue "string", "app_name", "@string/app_name_beta"
            versionNameSuffix = "-BETA_${releaseTime()}"
            applicationIdSuffix ".beta"

            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            matchingFallbacks = ['release', 'debug']
        }

        debug {
            resValue "string", "app_name", "@string/app_name_debug"
            versionNameSuffix = "-DEBUG_${releaseTime()}"
            applicationIdSuffix ".debug"

            signingConfig signingConfigs.release
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "LMusic_${variant.buildType.name}_${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/**']
        }
    }
    lint {
        checkDependencies true
    }
    lintOptions {
        disable "Instantiatable"
        abortOnError false
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd_HHmmZ", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    // https://github.com/aclassen/ComposeReorderable
    // https://mvnrepository.com/artifact/org.burnoutcrew.composereorderable/reorderable
    // Apache-2.0 license
    // Compose的拖动排序组件
    implementation 'org.burnoutcrew.composereorderable:reorderable:0.9.6'
    // 0.9.2对LazyColumn的ContentPadding存在偏移的bug

    // https://github.com/Block-Network/StatusBarApiExample
    // 墨 · 状态栏歌词 API
    implementation 'com.github.577fkj:StatusBarApiExample:v2.0'

    // https://mvnrepository.com/artifact/com.airbnb.android/lottie-compose
    implementation 'com.airbnb.android:lottie-compose:5.2.0'

    // https://github.com/FunnySaltyFish/ComposeDataSaver
    // Apache-2.0 License
    // implementation 'com.github.FunnySaltyFish.ComposeDataSaver:data-saver:v1.1.4'

    // https://google.github.io/accompanist
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"


    implementation "io.github.raamcosta.compose-destinations:animations-core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation "androidx.compose.compiler:compiler:$compose_compiler_version"
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-util'
    implementation 'androidx.compose.ui:ui-viewbinding'
    implementation 'androidx.compose.material3:material3-window-size-class'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-tooling-preview'

    // https://gitee.com/simplepeng/SpiderMan
    // Apache-2.0 License
    // 闪退崩溃日志捕获库
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'
    // debugImplementation 'com.github.markzhai:blockcanary-android:1.5.0'
    betaImplementation 'com.github.simplepeng.SpiderMan:spiderman:v1.1.9'
//    debugImplementation 'com.github.simplepeng.SpiderMan:spiderman:v1.1.9'

    // https://github.com/square/retrofit
    // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    // Apache-2.0 License
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // https://github.com/Commit451/NativeStackBlur
    // Apache-2.0 License
    // Bitmap的Blur实现库
    implementation 'com.github.Commit451:NativeStackBlur:1.0.4'

    // https://github.com/coil-kt/coil
    // Apache-2.0 License
    // 图片加载库
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    // https://github.com/Moriafly/LyricViewX
    // GPL-3.0 License
    // 歌词组件
    implementation 'com.github.cy745:LyricViewX:fe36e7bb64'

    // https://github.com/qinci/EdgeTranslucent
    // https://github.com/cy745/EdgeTranslucent
    // Undeclared License
    // 实现边沿渐变透明
    implementation 'com.github.cy745:EdgeTranslucent:8c25866a14'

    // Koin for Android
    // https://mvnrepository.com/artifact/io.insert-koin/koin-android
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_compose_version"

    // https://mvnrepository.com/artifact/androidx.datastore/datastore-preferences
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0-rc01'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.1.0-alpha03'
    implementation "androidx.navigation:navigation-compose:$navigation_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation project(":common")
    implementation project(":ui")
    implementation project(":lmedia")
}